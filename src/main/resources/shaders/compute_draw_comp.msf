#include math
#include random

#define PARALLEL_SAMPLES
#define PROGRESSIVE

#ifndef PARALLEL_SAMPLES
layout(local_size_x = 32, local_size_y = 32) in;
#else
layout(local_size_x = 4, local_size_y = 4, local_size_z = 64) in;
#endif

const uint BSDF_DIFFUSE = 0;
const uint BSDF_SPECULAR = 1;
const uint BSDF_REFRACT = 2;
const uint BSDF_DISNEY = 3;

struct Material
{
	uint bsdfType;
	vec3 radiance;
	vec3 albedo;
	float indexOfRefraction;
	DisneyParams disneyParams;
};

struct Ray
{
	vec3 origin;
	vec3 dir;
	vec3 invDir;
};

struct Sphere
{
	uint objectId;
	vec3 center;
	float radius;
	float area;
	Material material;
};

struct AABB
{
	uint objectId;
	vec3 min;
	vec3 max;
	float area;
	Material material;
};

struct Disk
{
	uint objectId;
	vec3 center;
	float radius;
	vec3 normal;
	float area;
	Material material;
};

struct Intersection
{
	uint objectId;
	vec3 pos;
	vec3 normal;
	bool inside;
	Material material;
};

Ray makeRay(const vec3 origin, const vec3 dir);
Sphere makeSphere(const vec3 center, const float radius, const Material material);
AABB makeAABB(const vec3 center, const vec3 size, const Material material);
Disk makeDisk(const vec3 center, const float radius, const vec3 normal, const Material material);

const Intersection NO_INTERSECTION = Intersection(-1, vec3(0), vec3(0), false, Material(0, vec3(0), vec3(0), 0, DISNEY_NULL));

const DisneyParams DISNEY_DEF = DisneyParams
(
	vec3(1,1,1), //vec3 baseColor;
	0, //float metallic;
	0, //float subsurface;
	0.5, //float specular;
	0.3, //float roughness;
	0, //float specularTint;
	0, //float anisotropic;
	0, //float sheen;
	.5, //float sheenTint;
	0, //float clearcoat;
	1 //float clearcoatGloss;
);

const int PATH_DEPTH = 5;

#ifndef PARALLEL_SAMPLES
const int SAMPLES_PER_PIXEL = 64;
#else
const int SAMPLES_PER_PIXEL = int(gl_WorkGroupSize.z);
#endif

const vec3 SUN_NORMAL = normalize(vec3(0.5, -0.5, -1));

layout(rgba32f) uniform restrict image2D framebuffer;

uniform int numPasses;
uniform float time;

uniform vec4 cam00;
uniform vec4 cam10;
uniform vec4 cam01;
uniform vec4 cam11;
uniform vec3 camPos;

uniform sampler2D environmentTex;

const uvec2 framebufferSize = imageSize(framebuffer);

uint pixelIndex;
uint sampleIndex;

uint nextObjectId = 0;

uvec4 randState;
uint quasiRandIndex;
uint quasiRandDimension = 0;

#ifdef PARALLEL_SAMPLES
shared vec3 samples[gl_WorkGroupSize.x * gl_WorkGroupSize.y * gl_WorkGroupSize.z];
#endif

Sphere spheres[] = Sphere[](
	//makeSphere(vec3(0,0,40), 100, Material(BSDF_DIFFUSE, vec3(0), vec3(1,0,0), 0, DISNEY_DEF)),

	makeSphere(vec3(0,-3,3), 2, Material(BSDF_DISNEY, vec3(0), vec3(1), 0, DISNEY_DEF)),
	makeSphere(vec3(-3,3,0), 2, Material(BSDF_SPECULAR, vec3(0), vec3(1), 0, DISNEY_DEF)),
	makeSphere(vec3(3,3,0), 2, Material(BSDF_SPECULAR, vec3(0), vec3(1), 0, DISNEY_DEF)),

	makeSphere(vec3(20,-5,-7), 7, Material(BSDF_DIFFUSE, vec3(0), vec3(1,0.5,0), 0, DISNEY_DEF))
);

AABB aabbs[] = AABB[](
	// World floor
	makeAABB(vec3(0, -6, 0), vec3(100, 0.1, 100), Material(BSDF_DISNEY, vec3(0), vec3(1), 0, DISNEY_DEF)),

	//makeAABB(vec3(0, 0, 40), vec3(100, 100, 200), Material(BSDF_DIFFUSE, vec3(0), vec3(1), 0, DISNEY_DEF))

	// Cornell box
	makeAABB(vec3(0, -5, 0), vec3(10, 0.1, 10), Material(BSDF_DISNEY, vec3(0), vec3(1), 0, DISNEY_DEF)), // Floor
	makeAABB(vec3(0, 0, -5), vec3(10, 10, 0.1), Material(BSDF_DISNEY, vec3(0), vec3(0,0,1), 0, DISNEY_DEF)), // Back
	makeAABB(vec3(-5, 0, 0), vec3(0.1, 10, 10), Material(BSDF_DISNEY, vec3(0), vec3(1,0,0), 0, DISNEY_DEF)), // Left
	makeAABB(vec3(5, 0, 0), vec3(0.1, 10, 10), Material(BSDF_DISNEY, vec3(0), vec3(0,1,0), 0, DISNEY_DEF)), // Right
	makeAABB(vec3(0, 5, 0), vec3(10, 0.1, 10), Material(BSDF_DISNEY, vec3(0), vec3(1), 0, DISNEY_DEF)), // Top
	
	makeAABB(vec3(3, -4, 0), vec3(3, 2, 10), Material(BSDF_REFRACT, vec3(0), vec3(1), 1.5, DISNEY_DEF)),
	makeAABB(vec3(0, 4.9, 0), vec3(1, 0.1, 1), Material(BSDF_DISNEY, vec3(0.2,0.5,2) * 0, vec3(1), 0, DISNEY_DEF)) // Light
);

Disk disks[] = Disk[](
	//makeDisk(-SUN_NORMAL * 149600e6, 695700e3, SUN_NORMAL, Material(BSDF_DIFFUSE, vec3(1,1,1) * 10000, vec3(1), 0, DISNEY_DEF)),
	makeDisk(vec3(10,-5,-10), 1, normalize(vec3(1,0,1)), Material(BSDF_DIFFUSE, vec3(10), vec3(1), 0, DISNEY_DEF)),
	makeDisk(vec3(0,2,0), 1, normalize(vec3(0,-1,1)), Material(BSDF_DIFFUSE, vec3(10), vec3(1), 0, DISNEY_DEF)),
	makeDisk(vec3(20,5,0), 1, normalize(vec3(-1,-0.8,-0.7)), Material(BSDF_DIFFUSE, vec3(30), vec3(1), 0, DISNEY_DEF))
	//makeDisk(vec3(0,10,0), 10, normalize(vec3(0,-1,0)), Material(BSDF_DIFFUSE, vec3(1), vec3(1), 0, DISNEY_DEF))
);

Ray makeRay(const vec3 origin, const vec3 dir)
{
	return Ray(origin, dir, 1 / dir);
}

Sphere makeSphere(const vec3 center, const float radius, const Material material)
{
	float area = 4 * PI * radius * radius;
	return Sphere(nextObjectId++, center, radius, area, material);
}

AABB makeAABB(const vec3 center, const vec3 size, const Material material)
{
	float area = 2 * (size.y * size.z + size.x * size.z + size.x * size.y);
	return AABB(nextObjectId++, center - size * 0.5, center + size * 0.5, area, material);
}

Disk makeDisk(const vec3 center, const float radius, const vec3 normal, const Material material)
{
	float area = PI * radius * radius;
	return Disk(nextObjectId++, center, radius, normal, area, material);
}

void initRand()
{
	uint invocationIndex = pixelIndex * SAMPLES_PER_PIXEL + sampleIndex;
	randState = 128 + wangHash(invocationIndex) + uvec4(wangHash(numPasses), wangHash(numPasses+1), wangHash(numPasses+2), wangHash(numPasses+3));
	quasiRandIndex = wangHash(pixelIndex) + SAMPLES_PER_PIXEL * (numPasses - 1) + sampleIndex;
}

uint randInt()
{
	return nextTausLCG(randState);
}

uint randInt(const uint bound)
{
	uint threshold = -bound % bound;
	while (true)
	{
		uint r = randInt();
		if (r >= threshold)
			return r % bound;
	}
}

float rand()
{
	return ldexp(randInt(), -32);
}

float quasiRand()
{
	return rand();
	//return ldexp(sobol(quasiRandDimension++, quasiRandIndex), -32);
}

bool intersectPlane(const vec3 center, const vec3 normal, const Ray r, out float t)
{
    float denom = dot(-normal, r.dir);
    if (denom > 1e-6)
    {
        t = dot(center - r.origin, -normal) / denom;
        return t >= 0;
    }
    return false;
}

void intersectSphere(const Sphere s, const Ray r, inout float maxDist, inout Intersection inter)
{
	vec3 p = r.origin - s.center;
	float b = -dot(p, r.dir);
	float d = b * b - dot(p, p) + s.radius * s.radius;
	if (d <= 0)
		return;

	d = sqrt(d);
	float tNear = b - d;
	float tFar = b + d;

    bool hit = tFar >= 0;
    float tHit = tNear < 0 ? tFar : tNear;
    if (!hit || tHit > maxDist)
    	return;

	vec3 hitPos = r.origin + r.dir * tHit;
	vec3 hitNormal = normalize(hitPos - s.center);
	bool inside = tNear < 0;
	if (inside)
		hitNormal = -hitNormal;

	inter = Intersection(s.objectId, hitPos, hitNormal, inside, s.material);
	maxDist = tHit;
}

void intersectAABB(const AABB aabb, const Ray r, inout float maxDist, inout Intersection inter)
{
	vec3 t1 = (aabb.min - r.origin) * r.invDir;
	vec3 t2 = (aabb.max - r.origin) * r.invDir;
 	vec3 tMin = min(t1, t2);
 	vec3 tMax = max(t1, t2);
 	float tNear = max(max(tMin.x, tMin.y), tMin.z);
 	float tFar = min(min(tMax.x, tMax.y), tMax.z);

    bool hit = tFar > 0 && tFar > tNear;
    float tHit = tNear < 0 ? tFar : tNear;
    if (!hit || tHit > maxDist)
    	return;

	vec3 hitPos = r.origin + r.dir * tHit;
 	vec3 hitNormal = vec3(0);
	bool inside = tNear < 0;

	if (tMin.x > tMin.y && tMin.x > tMin.z)
	{
		hitNormal = vec3(-sign(r.dir.x), 0, 0);
	}
	else if (tMin.y > tMin.x && tMin.y > tMin.z)
	{
		hitNormal = vec3(0, -sign(r.dir.y), 0);
	}
	else
	{
		hitNormal = vec3(0, 0, -sign(r.dir.z));
	}

	inter = Intersection(aabb.objectId, hitPos, hitNormal, inside, aabb.material);
	maxDist = tHit;
}

void intersectDisk(const Disk d, const Ray r, inout float maxDist, inout Intersection inter)
{
	float t;
	if (!intersectPlane(d.center, d.normal, r, t) || t > maxDist)
		return;

	vec3 hitPos = r.origin + r.dir * t;
	if (sq(hitPos - d.center) > d.radius * d.radius)
		return;

	vec3 hitNormal = d.normal * -sign(dot(r.dir, d.normal));
	inter = Intersection(d.objectId, hitPos, hitNormal, false, d.material);
	maxDist = t;
}

Intersection intersectScene(const Ray ray, float maxDist)
{
	Intersection inter = NO_INTERSECTION;
	for (int i = 0; i < spheres.length(); i++)
		intersectSphere(spheres[i], ray, maxDist, inter);
	for (int i = 0; i < aabbs.length(); i++)
		intersectAABB(aabbs[i], ray, maxDist, inter);
	for (int i = 0; i < disks.length(); i++)
		intersectDisk(disks[i], ray, maxDist, inter);

	return inter;
}

Intersection intersectScene(const Ray ray)
{
	return intersectScene(ray, INFINITY);
}

vec3 sampleBSDF(const Material material, const bool exiting, const vec3 normal, const vec3 Lo)
{
	switch(material.bsdfType)
	{
		default:
		case BSDF_DIFFUSE:
			return sampleHemisphereCosineWeighted(vec2(quasiRand(), quasiRand()), normal);
		case BSDF_SPECULAR:
			return reflect(-Lo, normal);
		case BSDF_REFRACT:
			float ior = exiting ? material.indexOfRefraction : 1 / material.indexOfRefraction;
			return refract(-Lo, normal, ior);
		case BSDF_DISNEY:

			vec3 tangent, bitangent;
			buildLocalFrame(tangent, bitangent, normal);

			return DisneyBRDFSample(normal, Lo, tangent, bitangent, vec3(quasiRand(), quasiRand(), quasiRand()), material.disneyParams);
	}
}

vec3 evaluateBSDF(const Material material, const vec3 Li, const vec3 normal, const vec3 Lo)
{
	switch(material.bsdfType)
	{
		default:
		case BSDF_DIFFUSE:
			return material.albedo / PI;
		case BSDF_SPECULAR:
			return material.albedo / dot(normal, Li); // TODO: Should be specular instead of albedo
		case BSDF_REFRACT:
			return material.albedo; // TODO: Wrong
		case BSDF_DISNEY:

			vec3 tangent, bitangent;
			buildLocalFrame(tangent, bitangent, normal);

			DisneyParams p = material.disneyParams;
			p.baseColor = material.albedo;
			return DisneyBRDF(Li, normal, Lo, tangent, bitangent, p);
	}
}

float evaluatePDF(const Material material, const vec3 Li, const vec3 normal, const vec3 Lo)
{
	switch(material.bsdfType)
	{
		default:
		case BSDF_DIFFUSE:
			return dot(normal, Li) / PI;
		case BSDF_SPECULAR:
			return 1;
		case BSDF_REFRACT:
			return 1;
		case BSDF_DISNEY:

			vec3 tangent, bitangent;
			buildLocalFrame(tangent, bitangent, normal);

			return DisneyBRDFPDF(Li, normal, Lo, tangent, bitangent, material.disneyParams);
	}
}

vec3 lightContribution(const uint objectId, const Material material, const vec3 lightRadiance, const vec3 pos, const float lightArea, const float lightDistSquared, const vec3 sampleDir, const vec3 normal, const vec3 Lo)
{
	vec3 light = vec3(0);

	// MIS: Sampling light
	{
		vec3 Li = sampleDir;

		Intersection lightInter = intersectScene(makeRay(pos + normal * EPSILON, Li), INFINITY);
		if (lightInter.objectId == objectId)
		{
			vec3 bsdf = evaluateBSDF(material, Li, normal, Lo);

			float lightPdf = lightDistSquared / (abs(dot(lightInter.normal, -Li)) * lightArea);
			float bsdfPdf = evaluatePDF(material, Li, normal, Lo);
			float weight = powerHeuristic(1, lightPdf, 1, bsdfPdf);

			light += bsdf * max(0, dot(normal, Li)) * lightRadiance * weight / lightPdf;
		}
	}

	// MIS: Sampling BSDF
	{
		vec3 Li = sampleBSDF(material, false, normal, Lo);

		Intersection lightInter = intersectScene(makeRay(pos + normal * EPSILON, Li), INFINITY);
		if (lightInter.objectId == objectId)
		{
			vec3 bsdf = evaluateBSDF(material, Li, normal, Lo);

			float lightPdf = lightDistSquared / (abs(dot(lightInter.normal, -Li)) * lightArea);
			float bsdfPdf = evaluatePDF(material, Li, normal, Lo);
			float weight = powerHeuristic(1, bsdfPdf, 1, lightPdf);

			light += bsdf * max(0, dot(normal, Li)) * lightRadiance * weight / bsdfPdf;
		}
	}

	return light;
}

vec3 directIllumination(const Material material, const vec3 pos, const vec3 normal, const vec3 Lo)
{
	vec3 light = vec3(0);

	for (int i = 0; i < spheres.length(); i++)
	{
		Sphere sphere = spheres[i];
		if (any(greaterThan(sphere.material.radiance, vec3(0))))
		{
			vec3 lightPos = sphere.center + sampleSphere(vec2(quasiRand(), quasiRand())) * sphere.radius;
			float lightDistSquared = sq(lightPos - pos);
			vec3 sampleDir = normalize(lightPos - pos);
			light += lightContribution(sphere.objectId, material, sphere.material.radiance, pos, sphere.area, lightDistSquared, sampleDir, normal, Lo);
		}
	}
	for (int i = 0; i < aabbs.length(); i++)
	{
		AABB aabb = aabbs[i];
		if (any(greaterThan(aabb.material.radiance, vec3(0))))
		{
			vec3 lightPos = vec3(0);

			uint side = randInt(6);
			switch (side)
			{
				case 0: // Bottom
					lightPos = vec3(mix(aabb.min.xz, aabb.max.xz, vec2(quasiRand(), quasiRand())), aabb.min.y).xzy;
					break;
				case 1: // Top
					lightPos = vec3(mix(aabb.min.xz, aabb.max.xz, vec2(quasiRand(), quasiRand())), aabb.max.y).xzy;
					break;
				case 2: // Left
					lightPos = vec3(mix(aabb.min.yz, aabb.max.yz, vec2(quasiRand(), quasiRand())), aabb.min.x).zxy;
					break;
				case 3: // Right
					lightPos = vec3(mix(aabb.min.yz, aabb.max.yz, vec2(quasiRand(), quasiRand())), aabb.max.x).zxy;
					break;
				case 4: // Back
					lightPos = vec3(mix(aabb.min.xy, aabb.max.xy, vec2(quasiRand(), quasiRand())), aabb.min.z).xyz;
					break;
				case 5: // Front
					lightPos = vec3(mix(aabb.min.xy, aabb.max.xy, vec2(quasiRand(), quasiRand())), aabb.max.z).xyz;
					break;
			}

			float lightDistSquared = sq(lightPos - pos);
			vec3 sampleDir = normalize(lightPos - pos);
			light += lightContribution(aabb.objectId, material, aabb.material.radiance, pos, aabb.area, lightDistSquared, sampleDir, normal, Lo);
		}
	}
	for (int i = 0; i < disks.length(); i++)
	{
		Disk disk = disks[i];
		if (any(greaterThan(disk.material.radiance, vec3(0))))
		{
			vec3 lightPos = disk.center + sampleDisk(vec2(quasiRand(), quasiRand()), disk.normal) * disk.radius;
			float lightDistSquared = sq(lightPos - pos);
			vec3 sampleDir = normalize(lightPos - pos);
			light += lightContribution(disk.objectId, material, disk.material.radiance, pos, disk.area, lightDistSquared, sampleDir, normal, Lo);
		}
	}

	return light;
}

vec3 background(const vec3 dir)
{
	return vec3(0);
	//return equirectangularMap(environmentTex, dir) * 0.7;
	//return mix(vec3(0,0,0), vec3(0.2,0.4,1) * 10, pow(dot(dir, vec3(0,1,0)) * 0.5 + 0.5, 5)) * 0.1;
}

vec3 sampleScene(const Ray startRay)
{
	vec3 color = vec3(0);
	vec3 throughput = vec3(1);
	
	Ray ray = startRay;
	bool specularBounce = false;
	for (int bounces = 0; ; bounces++)
	{
		Intersection inter = intersectScene(ray, INFINITY);

		if (inter.objectId == -1) // No intersection -> Background
		{
			color += throughput * background(ray.dir);
			break;
		}

		// Add material radiance
		if (bounces == 0 || specularBounce)
			color += throughput * inter.material.radiance;

		// Stop at max path depth
		if (bounces >= PATH_DEPTH)
			break;

		// Add direct lighting
		if (inter.material.bsdfType != BSDF_SPECULAR)
			color += throughput * directIllumination(inter.material, inter.pos, inter.normal, -ray.dir);

		// Sample BSDF for bounce ray direction and throughput
		vec3 Li = sampleBSDF(inter.material, inter.inside, inter.normal, -ray.dir);
		vec3 sidedNormal = inter.normal * sign(dot(inter.normal, Li));
		vec3 bsdf = evaluateBSDF(inter.material, Li, sidedNormal, -ray.dir);
		float pdf = evaluatePDF(inter.material, Li, sidedNormal, -ray.dir);
		if (bsdf == vec3(0) || pdf == 0)
			break;

		float NdotL = abs(dot(Li, inter.normal));
		throughput *= bsdf * (NdotL / pdf);

		// Russian Roulette
		if (bounces > 3)
		{
			float q = max(0.05, 1 - max(throughput.r, max(throughput.g, throughput.b)));
			if (quasiRand() < q)
				break;
			throughput /= 1 - q; // Re-add lost energy for terminated paths
		}

		ray = makeRay(inter.pos + sidedNormal * EPSILON, Li);
		specularBounce = inter.material.bsdfType == BSDF_SPECULAR;
	}

	return color;
}

vec3 samplePixel(const uvec2 coord)
{
	vec2 uv = (coord + vec2(quasiRand(), quasiRand())) / framebufferSize;
	vec3 dir = normalize(mix(mix(cam00.xyz, cam01.xyz, uv.y), mix(cam10.xyz, cam11.xyz, uv.y), uv.x));
	return sampleScene(makeRay(camPos, dir));
}

void saveSample(const uvec2 coord, const vec3 color)
{
#ifdef PROGRESSIVE

	vec3 prev = numPasses > 1 ? imageLoad(framebuffer, ivec2(coord)).rgb : vec3(0);
	vec3 newAvg = mix(prev, color, 1.0 / (SAMPLES_PER_PIXEL * numPasses));
	imageStore(framebuffer, ivec2(coord), vec4(newAvg, 1));

#else

	imageStore(framebuffer, ivec2(coord), vec4(color / SAMPLES_PER_PIXEL, 1));

#endif
}

void main()
{
	uvec2 pixelCoord = gl_GlobalInvocationID.xy;
	if (any(greaterThanEqual(pixelCoord, framebufferSize)))
		return;

	pixelIndex = gl_GlobalInvocationID.y * framebufferSize.x + gl_GlobalInvocationID.x;

#ifndef PARALLEL_SAMPLES

	vec3 color = vec3(0);
	for(int i = 0; i < SAMPLES_PER_PIXEL; i++)
	{
		sampleIndex = i;
		initRand();

		color += samplePixel(pixelCoord);
	}

	saveSample(pixelCoord, color);

#else

	sampleIndex = gl_GlobalInvocationID.z;
	initRand();

	vec3 sampleColor = samplePixel(pixelCoord);

	uint localSampleIndex = gl_LocalInvocationID.y * gl_WorkGroupSize.x * gl_WorkGroupSize.z + gl_LocalInvocationID.x * gl_WorkGroupSize.z + gl_LocalInvocationID.z;
	samples[localSampleIndex] = sampleColor;
	memoryBarrierShared();
	barrier();

	for (uint stride = gl_WorkGroupSize.z / 2; stride > 0; stride >>= 1)
	{
		if (gl_LocalInvocationID.z < stride)
			samples[localSampleIndex] += samples[localSampleIndex + stride];
		memoryBarrierShared();
		barrier();
	}

	if (gl_LocalInvocationID.z == 0)
	{
		vec3 color = samples[localSampleIndex];
		saveSample(pixelCoord, color);
	}

#endif
}
